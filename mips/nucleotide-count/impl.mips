
.globl nucleotide_counts

nucleotide_counts:
	li	$t0, 0	# A count
	li	$t1, 0	# C count
	li	$t2, 0	# G count
	li	$t3, 0	# T count
	move	$t4, $a0	# index to  current char in string
	
read_loop:
	lb	$t5, 0($t4)	# current input char
	addi	$t4, $t4, 1
	beqz	$t5, done 	# end of input string
check_A:	
	bne	$t5, 0x41, check_C
	addi	$t0, $t0, 1	# A += 1
	j	read_loop
check_C:	
	bne	$t5, 0x43, check_G
	addi	$t1, $t1, 1	# C += 1
	j	read_loop
check_G:	
	bne	$t5, 0x47, check_T
	addi	$t2, $t2, 1	# G += 1
	j	read_loop
check_T:	
	bne	$t5, 0x54, error
	addi	$t3, $t3, 1	# T += 1
	j	read_loop
	
error:	# invalid character found, set all counts to -1
	li	$t0, -1	# A count
	li	$t1, -1	# C count
	li	$t2, -1	# G count
	li	$t3, -1	# T count

done:	# - write counts for A,C,G,T to the word array with address given in a1
	sw	$t0, 0($a1)
	sw	$t1, 4($a1)
	sw	$t2, 8($a1)
	sw	$t3, 12($a1)
	
	jr 	$ra
	